apply plugin: 'maven'
apply plugin: 'signing' //使用signing plugin做数字签名

def gitUrl='https://github.com/hktianyi/TPlatform'
def scmGitUrl='scm:git:git@github.com:hktianyi/TPlatform.git'

repositories {
    mavenLocal()
    maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
}

uploadArchives {
    repositories {
        mavenDeployer {
            //为Pom文件做数字签名
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

//            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
//                authentication(userName: sonatypeUsername, password: sonatypePassword)
//            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            addFilter('jar') {artifact, file ->
                artifact.ext == 'jar'
            }

            pom.project {
                name project.name
                packaging 'jar'
                description 'TPlatform'
                url gitUrl

                issueManagement {
                    url gitUrl + '/issues'
                    system 'GitHub Issues'
                }

                scm {
                    url gitUrl
                    connection scmGitUrl
                    developerConnection scmGitUrl
                }

                licenses {
                    license {
                        name 'GNU GENERAL PUBLIC LICENSE, Version 3.0'
                        url 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id sonatypeUsername
                        name 'Tianyi'
                        email 'hk_tianyi@163.com'
                    }
                }
            }
        }
    }
}

//添加UTF-8编码否则注释可能JAVADOC文档可能生成不了
javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/8/docs/api/"
        title "swipeJavaDoc"
    }
}

// 为项目生成**.jar/**-javadoc.jar/**-sources.jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

// 为所有的jar包做数字签名
signing {
    sign configurations.archives
}
